plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'maven-publish'
    id 'signing'
}

apply plugin: "jacoco"
jacoco {
    toolVersion = "0.8.7"
}

application {
    mainClass.set("application.SpecmaticApplication")
}

jar {
    from '../version.properties'
}

shadowJar {
    archiveFileName = "specmatic.${archiveExtension.get()}"
    from '../version.properties'
}

repositories {
    mavenLocal()
    mavenCentral()
}

def ktor_version = "2.3.7"
def junit_version = "5.10.1"
def jgit_version = "6.8.0.202311291450-r"

dependencies {
    implementation 'com.arakelian:java-jq:1.3.0'
    testImplementation 'com.arakelian:java-jq:1.3.0'

    implementation 'org.assertj:assertj-core:3.25.2'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'

    implementation('info.picocli:picocli-spring-boot-starter:4.7.5') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    implementation 'info.picocli:picocli:4.7.5'
    implementation "io.ktor:ktor-client-core-jvm:${ktor_version}"
    implementation "io.ktor:ktor-network-tls:$ktor_version"
    implementation "io.ktor:ktor-network-tls-certificates:$ktor_version"

    implementation 'org.junit.platform:junit-platform-launcher:1.10.1'
    implementation 'org.junit.platform:junit-platform-reporting:1.10.1'

    implementation "org.eclipse.jgit:org.eclipse.jgit:$jgit_version"
    implementation "org.eclipse.jgit:org.eclipse.jgit.ssh.apache:$jgit_version"

    implementation 'org.slf4j:slf4j-nop:2.0.11'

    implementation 'org.apache.ant:ant-junit:1.10.14'

    implementation(project(':core'))
    implementation(project(':junit5-support'))

// https://mvnrepository.com/artifact/org.fusesource.jansi/jansi
    implementation 'org.fusesource.jansi:jansi:2.4.1'

    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation 'io.swagger.parser.v3:swagger-parser:2.1.20'

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json-jvm:1.6.2"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"

    testImplementation('org.springframework.boot:spring-boot-starter-test:3.2.2') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude module: 'mockito-core'
    }
    testImplementation 'io.mockk:mockk:1.13.9'
    testImplementation('com.ninja-squad:springmockk:3.1.1') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    testImplementation 'org.assertj:assertj-core:3.25.2'
    testImplementation 'com.ginsberg:junit5-system-exit:1.1.2'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = false
        csv.required = false
    }
}

group = 'in.specmatic'

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "17"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
